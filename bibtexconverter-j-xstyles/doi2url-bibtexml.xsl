<?xml version="1.0" encoding="UTF-8" ?>
<!-- $Id: bibxml2mods32.xsl 338 2007-08-27 17:27:58Z ringler $
     (c) Moritz Ringler, 2007

      XSLT stylesheet that turn converts bibtexml to bibtexml
      turning dois into urls using the

  I wrote this style-sheet to generate bib files that can be used with
  bibtex styles generated by urlbst.
  When you enable dois using the urlbst -doi option, urlbst will put
  the doi in the bibliography even when -inline is specified. Therefore,
  the dois must first be turned into urls which will be invisible with
  -inline.

 (c) Moritz Ringler, 2007

 This program is free software; you can redistribute it and/or
 modify it under the terms of the GNU General Public License
 as published by the Free Software Foundation; either version 2
 of the License, or (at your option) any later version.

 This program is distributed in the hope that it will be useful,
 but WITHOUT ANY WARRANTY; without even the implied warranty of
 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 GNU General Public License for more details.

 You should have received a copy of the GNU General Public License
 along with this program; if not, write to the Free Software
 Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
-->
<xsl:transform version="2.0"
    xmlns:bibtex="http://bibtexml.sf.net/"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:xs="http://www.w3.org/2001/XMLSchema"
    xmlns:foo="http://foo/bar/baz"
    exclude-result-prefixes="bibtex xs foo">
    <xsl:param name="doiproxy" select="'http://dx.doi.org/'" as="xs:string"/>

  <xsl:output
      method="xml"
      indent="yes"
      version="1.0"/>

  <!-- Document root -->
  <xsl:template match="/">
    <xsl:apply-templates />
  </xsl:template>

  <xsl:template match="bibtex:doi">
    <bibtex:url><xsl:value-of select="foo:doi-to-url(text(), $doiproxy)" /></bibtex:url>
  </xsl:template>

  <xsl:template match="bibtex:isbn">
    <bibtex:url><xsl:value-of
      select="concat('http://www.openurl.de/?isbn=', normalize-space(text()))"/>
    </bibtex:url>
  </xsl:template>

  <xsl:template match="*|text()|@*">
    <xsl:copy>
      <xsl:apply-templates select="@*" />
      <xsl:apply-templates />
    </xsl:copy>
  </xsl:template>

  <xsl:function name="foo:normalize-doi">
    <xsl:param name="doi" as="xs:string" />
    <!-- Step 1 : remove leading and trailing whitespace -->
    <xsl:variable name="step1" select="normalize-space($doi)"/>
    <!-- Step 2 : remove a possible leading "urn:doi:" -->
    <xsl:variable name="step2"
      select="if (starts-with($step1, 'urn:doi:')) then substring-after($step1, 'urn:doi:') else $step1"/>
    <!-- Step 3 : remove a possible leading "doi:" -->
    <xsl:sequence
      select="if (starts-with($step1, 'doi:')) then substring-after($step1, 'doi:') else $step2"/>
  </xsl:function>

  <xsl:function name="foo:doi-to-url">
    <xsl:param name="doi" as="xs:string" />
    <xsl:param name="proxy" as="xs:string" />
    <xsl:sequence select="concat($proxy, foo:normalize-doi($doi))"/>
  </xsl:function>

  <xsl:function name="foo:doi-to-url">
    <xsl:param name="doi" as="xs:string" />
    <xsl:sequence select="foo:doi-to-url($doi, 'http://dx.doi.org/')"/>
  </xsl:function>

</xsl:transform>
